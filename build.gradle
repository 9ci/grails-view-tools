buildscript {
	repositories {
		mavenLocal()
		maven { url "https://repo.grails.org/grails/core" }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
		classpath "org.grails:grails-docs:$grailsVersion"
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
		classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
		classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.8.2"
	}
}
repositories {
	mavenLocal()
	maven { url "https://repo.grails.org/grails/core" }
}

version project.projectVersion

ext {
	commonBuild = projectDir
	grailsApps = ['test-app']
	grailsPlugins = ['view-tools', 'foobar-plugin']
}


subprojects {

	boolean isGrailsApp = grailsApps.contains(project.name)
	boolean isGrailsPlugin = grailsPlugins.contains(project.name)
	boolean isGrailsProject = isGrailsApp || isGrailsPlugin
	boolean isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")

	ext {
		userOrg = "grails"
		set "tomcat.version", "8.5.5"
		isSnapshot = isBuildSnapshot
	}

	apply from:"$commonBuild/common.gradle"

	if (isGrailsPlugin) {
		apply from: "${commonBuild}/common-plugin.gradle"
		apply from: "${commonBuild}/common-publishing.gradle"
		group "org.grails.plugins"
	}

	if (isGrailsApp) {
		apply from: "${commonBuild}/common-project.gradle"
		group "org.grails"
	}

}